buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

repositories {
    jcenter()
    maven {
        url "../m2repository"
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    sourceSets {
        instrumentTest {
            assets.srcDirs    = ["test/assets"]
            res.srcDirs       = ["test/res"]
            resources.srcDirs = ["test/src"]
            java.srcDirs      = ["test/src"]
        }
    }
}

configurations {
    compile.exclude group: 'org.json', module: 'json'
    androidTestCompile.exclude group: 'org.json', module: 'json'
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'jp.fkmsoft.libs:KiiLib-Java-Core:3.0.0'
    compile 'org.json:json:20090211'
    testCompile 'junit:junit:4.11'
    testCompile 'jp.fkmsoft.libs:KiiLib-Java-TestTools:3.0.0'
}

uploadArchives {
    repositories.mavenDeployer {
        repository url: "file://$System.env.HOME/.m2/repository"
        pom.version = '3.0.0'
        pom.groupId = 'jp.fkmsoft.libs'
        pom.artifactId = 'KiiLib-Android-Entity'
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}